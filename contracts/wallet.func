#include "imports/stdlib.fc";
#include "libs/utils.func";
#include "standard/op.func";

#include "common/op.func";
#include "common/errors.func";

#include "minter/op.func";
#include "minter/params.func";

#include "wallet/params.func";
#include "wallet/storage.func";
#include "wallet/errors.func";

global int balance;
global int withdraw_cnt;
global slice owner_address;
global slice jetton_master_address;
global cell jetton_wallet_code;

() load_global_data() impure inline {
    (
        balance, 
        withdraw_cnt, 
        owner_address, 
        jetton_master_address, 
        jetton_wallet_code
    ) = load_data();
}

() save_global_data() impure inline {
    save_data(
        balance, 
        withdraw_cnt, 
        owner_address, 
        jetton_master_address, 
        jetton_wallet_code
    );
}

() send_tokens (slice in_msg_body, slice sender_address, int msg_value, int fwd_fee) impure {
    int query_id = in_msg_body~load_uint(64);
    int uton_amount = in_msg_body~load_coins();
    slice to_owner_address = in_msg_body~load_msg_addr();
    force_chain(COMMON::WORKCHAIN, to_owner_address, 704);

    load_global_data();
    balance -= uton_amount;
    throw_unless(WALLET::ERR::SENDER_NOT_OWNER, equal_slices(owner_address, sender_address));
    throw_unless(WALLET::ERR::BALANCE_NOT_ENOUGH, balance >= 0);

    cell state_init = calculate_jetton_wallet_state_init(to_owner_address, jetton_master_address, jetton_wallet_code);
    slice to_wallet_address = calculate_jetton_wallet_address(state_init);
    slice response_address = in_msg_body~load_msg_addr();
    cell custom_payload = in_msg_body~load_dict();
    int forward_ton_amount = in_msg_body~load_coins();
    throw_unless(WALLET::ERR::MSG_BODY_PARTIAL_LOSS, slice_bits(in_msg_body) >= 1);
    
    slice either_forward_payload = in_msg_body;
    var msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(to_wallet_address)
        .store_coins(0)
        .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
        .store_ref(state_init);
    var msg_body = begin_cell()
        .store_uint(JETTON::OP::INTERNAL_TRANSFER, 32)
        .store_uint(query_id, 64)
        .store_coins(uton_amount)
        .store_slice(owner_address)
        .store_slice(response_address)
        .store_coins(forward_ton_amount)
        .store_slice(either_forward_payload)
        .end_cell();

    msg = msg.store_ref(msg_body);
    int fwd_count = forward_ton_amount ? 2 : 1;
    throw_unless(
        WALLET::ERR::INSUFFICIENT_VALUE, 
        msg_value > forward_ton_amount 
            + fwd_count * fwd_fee 
            + WALLET::SEND_FEE
            + WALLET::RECEIVE_FEE 
            + WALLET::MIN_TON_STORAGE
        );

    save_global_data();
    ;; send JETTON::INTERNAL_TRANSFER, msg-64
    send_raw_message(msg.end_cell(), 64); ;; revert on errors
}

() receive_tokens (slice in_msg_body, slice sender_address, int my_ton_balance, int fwd_fee, int msg_value) impure {
    load_global_data();

    int query_id = in_msg_body~load_uint(64);
    int uton_amount = in_msg_body~load_coins();
    balance += uton_amount;

    slice from_address = in_msg_body~load_msg_addr();
    slice response_address = in_msg_body~load_msg_addr();
    throw_unless(
        WALLET::ERR::SENDER_NOT_JETTON_MASTER_OR_NOT_WALLET, 
        equal_slices(calculate_user_jetton_wallet_address(from_address, jetton_master_address, jetton_wallet_code), sender_address)
        |
        equal_slices(jetton_master_address, sender_address)
    );
    int forward_ton_amount = in_msg_body~load_coins();

    int ton_balance_before_msg = my_ton_balance - msg_value;
    int storage_fee = WALLET::MIN_TON_STORAGE - min(ton_balance_before_msg, WALLET::MIN_TON_STORAGE);
    msg_value -= (storage_fee + WALLET::RECEIVE_FEE);
    if(forward_ton_amount) {
        msg_value -= (forward_ton_amount + fwd_fee);
        slice either_forward_payload = in_msg_body;

        var msg_body = begin_cell()
            .store_uint(JETTON::OP::TRANSFER_NOTIFICATION, 32)
            .store_uint(query_id, 64)
            .store_coins(uton_amount)
            .store_slice(from_address)
            .store_slice(either_forward_payload)
            .end_cell();

        var msg = begin_cell()
            .store_uint(0x18, 6)
            .store_slice(owner_address)
            .store_coins(forward_ton_amount)
            .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_ref(msg_body);

        send_raw_message(msg.end_cell(), 1);
    }

    if ((response_address.preload_uint(2) != 0) & (msg_value > 0)) {
        var msg = begin_cell()
            .store_uint(0x10, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 011000
            .store_slice(response_address)
            .store_coins(msg_value)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(OP::EXCESSES, 32)
            .store_uint(query_id, 64);
        send_raw_message(msg.end_cell(), 2);
    }

    save_global_data();
}

() burn_tokens (slice in_msg_body, slice sender_address, int my_ton_balance, int fwd_fee, int msg_value) impure {
    
    load_global_data();

    int query_id = in_msg_body~load_uint(64);
    int uton_amount = in_msg_body~load_coins();
    slice response_address = in_msg_body~load_msg_addr();
    cell custom_payload = in_msg_body~load_dict();
    (cell payload_ref, int has_payload) = custom_payload.udict_get_ref?(32, 0);
    throw_unless(WALLET::ERR::MSG_BODY_PARTIAL_LOSS, has_payload);
    slice payload = payload_ref.begin_parse();
    int proxy_id = payload~load_uint(32);
    int fwd_value = payload~load_coins();
    throw_unless(
        WALLET::ERR::INVALID_FWD_FEE,
        fwd_value > 0
    );
    cell fwd_msg_body = payload~load_ref();
    int withdraw_id = withdraw_cnt;
    withdraw_cnt += 1;
    balance -= uton_amount;

    throw_unless(
        WALLET::ERR::SENDER_NOT_OWNER, 
        equal_slices(owner_address, sender_address)
    );
    throw_unless(
        WALLET::ERR::BALANCE_NOT_ENOUGH, 
        balance >= 0
    );

    int ton_balance_before_msg = my_ton_balance - msg_value;
    int storage_fee = WALLET::MIN_TON_STORAGE - min(ton_balance_before_msg, WALLET::MIN_TON_STORAGE);

    throw_unless(
        WALLET::ERR::INSUFFICIENT_VALUE, 
        msg_value > storage_fee
            + WALLET::BURN_FEE 
            + fwd_fee 
            + MINTER::BURN_FEE
            + fwd_value
    );

    msg_value -= (storage_fee + WALLET::BURN_FEE);

    save_global_data();

    var msg_body = begin_cell()
        .store_uint(JETTON::OP::BURN_NOTIFICATION, 32)
        .store_uint(query_id, 64)
        .store_slice(owner_address)
        .store_uint(withdraw_id, 64)
        .store_coins(uton_amount)
        .store_uint(proxy_id, 32)
        .store_slice(response_address)
        .store_coins(fwd_value)
        .store_ref(fwd_msg_body)
        .end_cell();

    var msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(jetton_master_address)
        ;; todo determine msg value for burn notification
        .store_coins(msg_value)
        .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_ref(msg_body);
    send_raw_message(msg.end_cell(), 0);

}

() on_bounce (slice in_msg_body) impure {
    in_msg_body~skip_bits(32); ;; 0xFFFFFFFF
    load_global_data();
    int op = in_msg_body~load_uint(32);
    throw_unless(WALLET::ERR::BOUNCE_ERR_INVALID_OP, (op == JETTON::OP::INTERNAL_TRANSFER) | (op == JETTON::OP::BURN_NOTIFICATION));
    int query_id = in_msg_body~load_uint(64);
    int uton_amount = in_msg_body~load_coins();
    balance += uton_amount;
    ;; cannot reduce withdraw_cnt back
    save_global_data();
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) {
        on_bounce(in_msg_body);
        return ();
    }
    slice sender_address = cs~load_msg_addr();
    cs~load_msg_addr();
    cs~load_coins();
    cs~skip_bits(1);
    cs~load_coins();
    int fwd_fee = cs~load_coins();

    int op = in_msg_body~load_uint(32);

    if (op == JETTON::OP::TRANSFER) { 
        send_tokens(in_msg_body, sender_address, msg_value, fwd_fee);
        return ();
    }

    if (op == JETTON::OP::INTERNAL_TRANSFER) {
        receive_tokens(in_msg_body, sender_address, my_balance, fwd_fee, msg_value);
        return ();
    }

    if (op == JETTON::OP::BURN) {
        burn_tokens(in_msg_body, sender_address, my_balance, fwd_fee, msg_value);
        return ();
    }

    throw(0xffff);
}

(int, slice, slice, cell) get_wallet_data() method_id {
    load_global_data();
    return (
        balance, 
        owner_address, 
        jetton_master_address, 
        jetton_wallet_code
    );
}

(int, int, slice, slice, cell) get_complete_wallet_data() method_id {
    load_global_data();
    return (
        balance, 
        withdraw_cnt,
        owner_address, 
        jetton_master_address, 
        jetton_wallet_code
    );
}
